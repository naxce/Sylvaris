self:
{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.programs.sylvaris;
  json = pkgs.formats.json { };
  parts = (cfg.settings.parts or { }) // cfg.parts;
in
{
  options.programs.sylvaris = {
    enable = lib.mkEnableOption "Sylvaris, a modular Quickshell desktop shell";

    package = lib.mkOption {
      type = lib.types.package;
      default = self.packages.${pkgs.stdenv.hostPlatform.system}.sylvaris;
      defaultText = lib.literalExpression "sylvaris.packages.\${system}.sylvaris";
      description = "The Sylvaris package to install.";
    };

    settings = lib.mkOption {
      type = json.type;
      default = { };
      description = "Written to ~/.config/sylvaris/config.json. See the README for every key.";
    };

    parts = lib.mkOption {
      type = lib.types.attrsOf lib.types.bool;
      default = { };
      example = {
        pad = false;
        media = false;
      };
      description = "Parts to exclude (false) or keep (true), written to the parts key of config.json. Tools only excluded parts need are left off the package's PATH.";
    };

    themes = lib.mkOption {
      type = lib.types.attrsOf json.type;
      default = { };
      description = "Theme bundles written to ~/.config/sylvaris/themes/<name>.json. The attribute name becomes the theme id.";
    };
  };

  config = lib.mkIf cfg.enable {
    assertions = [
      {
        assertion = lib.all (name: lib.elem name cfg.package.parts) (lib.attrNames parts);
        message = "programs.sylvaris.parts: unknown part; valid parts are ${lib.concatStringsSep ", " cfg.package.parts}";
      }
    ];

    home.packages = [ (cfg.package.override { sylvarisParts = parts; }) ];

    xdg.configFile = lib.mkMerge [
      {
        "sylvaris/config.json".source = json.generate "sylvaris-config.json" (
          { version = 1; } // cfg.settings // lib.optionalAttrs (parts != { }) { inherit parts; }
        );
      }
      (lib.mapAttrs' (
        name: theme:
        lib.nameValuePair "sylvaris/themes/${name}.json" {
          source = json.generate "sylvaris-theme-${name}.json" (
            {
              version = 1;
              id = name;
            }
            // theme
          );
        }
      ) cfg.themes)
    ];
  };
}
